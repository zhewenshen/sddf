/*
 * Copyright 2025, UNSW
 * SPDX-License-Identifier: BSD-2-Clause
 */

#define CONFIG_CLIENT_ID         0
#define CONFIG_VIRT_RX_ID        1
#define CLI_QUEUE_BASE           10
#define VIRT_QUEUE_BASE          30
#define CLIENT_DATA_VADDR        200
#define DEVICE_DATA_VADDR        201
#define CLI_NUM_BUFFERS          202
#define VIRT_NUM_BUFFERS         203

// Buffer constants
#define NET_BUFFER_SIZE          2048
#define NET_BUFFER_SHIFT         11
#define NET_BUFFER_MASK          2047

// Global variables - loaded once in init
var g_client_id = 0;
var g_virt_rx_id = 0;

var g_cli_free_queue = 0;
var g_cli_active_queue = 0;
var g_cli_capacity = 0;
var g_cli_num_buffers = 0;

var g_virt_free_queue = 0;
var g_virt_active_queue = 0;
var g_virt_capacity = 0;

var g_client_data_vaddr = 0;
var g_device_data_vaddr = 0;

fun load_globals() {
    g_client_id = pnk_mem(CONFIG_CLIENT_ID);
    g_virt_rx_id = pnk_mem(CONFIG_VIRT_RX_ID);

    g_cli_free_queue = pnk_mem(CLI_QUEUE_BASE);
    g_cli_active_queue = pnk_mem(CLI_QUEUE_BASE + 1);
    g_cli_capacity = pnk_mem(CLI_QUEUE_BASE + 2);
    g_cli_num_buffers = pnk_mem(CLI_NUM_BUFFERS);

    g_virt_free_queue = pnk_mem(VIRT_QUEUE_BASE);
    g_virt_active_queue = pnk_mem(VIRT_QUEUE_BASE + 1);
    g_virt_capacity = pnk_mem(VIRT_QUEUE_BASE + 2);

    g_client_data_vaddr = pnk_mem(CLIENT_DATA_VADDR);
    g_device_data_vaddr = pnk_mem(DEVICE_DATA_VADDR);

    return 0;
}

fun rx_return() {
    var client_enqueued = 0;
    var virt_enqueued = 0;
    var reprocess = 1;

    // Cache hot globals into locals
    var virt_active = g_virt_active_queue;
    var virt_cap = g_virt_capacity;
    var virt_free = g_virt_free_queue;
    var cli_free = g_cli_free_queue;
    var cli_active = g_cli_active_queue;
    var cli_cap = g_cli_capacity;
    var cli_data = g_client_data_vaddr;
    var dev_data = g_device_data_vaddr;
    var max_offset = g_cli_num_buffers << NET_BUFFER_SHIFT;

    while (reprocess) {
        var 1 virt_active_empty = net_queue_empty(virt_active, virt_cap);

        while (virt_active_empty == 0) {
            var 1 cli_free_empty = net_queue_empty(cli_free, cli_cap);

            if (cli_free_empty == 0) {
                var {1,1} cli_buffer = net_dequeue(cli_free, cli_cap);
                var cli_io_or_offset = cli_buffer.0;
                var cli_len = cli_buffer.1;

                var cli_offset_check = cli_io_or_offset & NET_BUFFER_MASK;

                if (cli_offset_check == 0 && cli_io_or_offset < max_offset) {
                    var {1,1} virt_buffer = net_dequeue(virt_active, virt_cap);
                    var virt_io_or_offset = virt_buffer.0;
                    var virt_len = virt_buffer.1;

                    var cli_addr = cli_data + cli_io_or_offset;
                    var virt_addr = dev_data + virt_io_or_offset;

                    @sddf_memcpy(cli_addr, virt_len, virt_addr, 0);

                    cli_len = virt_len;
                    virt_len = 0;

                    net_enqueue(cli_active, cli_io_or_offset, cli_len, cli_cap);
                    net_enqueue(virt_free, virt_io_or_offset, virt_len, virt_cap);

                    client_enqueued = 1;
                } else {
                    var {1,1} virt_buffer = net_dequeue(virt_active, virt_cap);
                    var virt_io_or_offset = virt_buffer.0;

                    net_enqueue(virt_free, virt_io_or_offset, 0, virt_cap);
                }
            } else {
                var {1,1} virt_buffer = net_dequeue(virt_active, virt_cap);
                var virt_io_or_offset = virt_buffer.0;

                net_enqueue(virt_free, virt_io_or_offset, 0, virt_cap);
            }

            virt_enqueued = 1;

            virt_active_empty = net_queue_empty(virt_active, virt_cap);
        }

        net_request_signal(virt_active, virt_cap);
        reprocess = 0;

        virt_active_empty = net_queue_empty(virt_active, virt_cap);
        if (virt_active_empty == 0) {
            net_cancel_signal(virt_active);
            reprocess = 1;
        }
    }

    if (client_enqueued) {
        var 1 client_signal_required = net_require_signal(cli_active, cli_cap);

        if (client_signal_required) {
            net_cancel_signal(cli_active);
            sddf_notify(g_client_id)
        }
    }

    if (virt_enqueued) {
        var 1 virt_signal_required = net_require_signal(virt_free, virt_cap);

        if (virt_signal_required) {
            net_cancel_signal(virt_free);
            sddf_deferred_notify(g_virt_rx_id)
        }
    }

    return 0;
}

export fun notified(1 ch) {
    rx_return();
    return 0;
}

export fun init_pnk() {
    load_globals();
    return 0;
}
