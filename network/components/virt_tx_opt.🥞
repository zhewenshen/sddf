/*
 * Copyright 2025, UNSW
 * SPDX-License-Identifier: BSD-2-Clause
 */

#define CONFIG_DRIVER_ID         pnk_mem(0)
#define DRV_QUEUE_BASE           10
#define CLI_QUEUE_BASE           30
#define CLI_DATA_IO_BASE         100
#define CLI_CONN_ID_BASE         150
#define NUM_CLIENTS              200
#define NOTIFY_CLIENTS_BASE      210

#define NET_BUFF_DESC_SIZE       16
#define MAX_INT16                65535

var 1 config_driver_id    = pnk_mem(CONFIG_DRIVER_ID);
var 1 num_clients         = pnk_mem(NUM_CLIENTS);
var 1 drv_active_queue    = pnk_mem(DRV_QUEUE_BASE + 1);
var 1 drv_capacity        = pnk_mem(DRV_QUEUE_BASE + 2);
var 1 drv_free_queue      = pnk_mem(DRV_QUEUE_BASE);

#define NET_QUEUE_EMPTY(queue_ptr, capacity, result_out) \
  qe_tail = 0; \
  qe_head = 0; \
  !ld16 qe_tail, queue_ptr; \
  !ld16 qe_head, queue_ptr + 2; \
  qe_empty_check = qe_tail - qe_head; \
  result_out = (qe_empty_check == 0)

#define NET_DEQUEUE(queue_ptr, capacity, io_out, len_out) \
  dq_head = 0; \
  !ld16 dq_head, queue_ptr + 2; \
  dq_mod_head = dq_head & (capacity - 1); \
  dq_entry_addr = queue_ptr + 8 + dq_mod_head * NET_BUFF_DESC_SIZE; \
  !ldw io_out, dq_entry_addr; \
  !ld32 len_out, dq_entry_addr + 8; \
  dq_head = (dq_head + 1) & MAX_INT16; \
  !st16 queue_ptr + 2, dq_head

#define NET_ENQUEUE(queue_ptr, io_addr, len, capacity) \
  eq_tail = 0; \
  !ld16 eq_tail, queue_ptr; \
  eq_mod_tail = eq_tail & (capacity - 1); \
  eq_entry_addr = queue_ptr + 8 + eq_mod_tail * NET_BUFF_DESC_SIZE; \
  !stw eq_entry_addr, io_addr; \
  !st32 eq_entry_addr + 8, len; \
  eq_tail = (eq_tail + 1) & MAX_INT16; \
  !st16 queue_ptr, eq_tail

#define NET_REQUEST_SIGNAL(queue_ptr) \
  !st32 queue_ptr + 4, 0

#define NET_CANCEL_SIGNAL(queue_ptr) \
!st32 queue_ptr + 4, 1

#define NET_REQUIRE_SIGNAL(queue_ptr, result_out) \
  rs_signalled = 0; \
  !ld32 rs_signalled, queue_ptr + 4; \
  result_out = !rs_signalled

#define EXTRACT_OFFSET(phys_addr, num_clients, client_out, offset_out) \
  eo_client = 0; \
  client_out = -1; \
  offset_out = 0; \
  while (eo_client < num_clients) { \
    if (client_out >= 0) {break;} \
    eo_cli_data_io = pnk_mem(CLI_DATA_IO_BASE + eo_client); \
    eo_cli_capacity = pnk_mem(CLI_QUEUE_BASE + eo_client * 4 + 2); \
    eo_cli_max_addr = eo_cli_data_io + eo_cli_capacity * 2048; \
    if (phys_addr >= eo_cli_data_io && phys_addr < eo_cli_max_addr) { \
      offset_out = phys_addr - eo_cli_data_io; \
      client_out = eo_client; \
    } \
    eo_client = eo_client + 1; \
  }

fun tx_provide() {
    var enqueued = 0;
    var client = 0;

    var local_num_clients        = num_clients;
    var local_drv_active_queue   = drv_active_queue;
    var local_drv_capacity       = drv_capacity;

    var qe_tail = 0;
    var qe_head = 0;
    var qe_empty_check = 0;
    var dq_head = 0;
    var dq_mod_head = 0;
    var dq_entry_addr = 0;
    var eq_tail = 0;
    var eq_mod_tail = 0;
    var eq_entry_addr = 0;
    var rs_signalled = 0;

    while (client < local_num_clients) {
        var reprocess = 1;

        /* Load client queue info once per client */
        var cli_queue_base = CLI_QUEUE_BASE + client * 4;
        var cli_active_queue = pnk_mem(cli_queue_base + 1);
        var cli_capacity = pnk_mem(cli_queue_base + 2);
        var cli_free_queue = pnk_mem(cli_queue_base);
        var cli_data_vaddr = pnk_mem(cli_queue_base + 3);
        var cli_data_io = pnk_mem(CLI_DATA_IO_BASE + client);

        while (reprocess) {
            var cli_empty = 0;
            NET_QUEUE_EMPTY(cli_active_queue, cli_capacity, cli_empty);

            while (1) {
                if (cli_empty) { break; }

                var io_offset = 0;
                var len = 0;
                NET_DEQUEUE(cli_active_queue, cli_capacity, io_offset, len);

                var aligned = (io_offset & 2047) == 0;
                var in_bounds = io_offset < (cli_capacity * 2048);

                if (!aligned || !in_bounds) {
                    NET_ENQUEUE(cli_free_queue, io_offset, len, cli_capacity);
                } else {
                    var buffer_vaddr = cli_data_vaddr + io_offset;

                    cache_clean(buffer_vaddr, buffer_vaddr + len)

                    var phys_addr = io_offset + cli_data_io;
                    NET_ENQUEUE(local_drv_active_queue, phys_addr, len, local_drv_capacity);
                    enqueued = 1;
                }

                NET_QUEUE_EMPTY(cli_active_queue, cli_capacity, cli_empty);
            }

            NET_REQUEST_SIGNAL(cli_active_queue);
            reprocess = 0;

            NET_QUEUE_EMPTY(cli_active_queue, cli_capacity, cli_empty);
            if (cli_empty == 0) {
                NET_CANCEL_SIGNAL(cli_active_queue);
                reprocess = 1;
            }
        }

        client = client + 1;
    }

    if (enqueued) {
        var signal_required = 0;
        NET_REQUIRE_SIGNAL(local_drv_active_queue, signal_required);

        if (signal_required) {
            NET_CANCEL_SIGNAL(local_drv_active_queue);
            sddf_deferred_notify(config_driver_id)
        }
    }

    return 0;
}

fun tx_return() {
    var reprocess = 1;
    var i = 0;

    var local_num_clients        = num_clients;
    var local_drv_free_queue     = drv_free_queue;
    var local_drv_capacity       = drv_capacity;

    var qe_tail = 0;
    var qe_head = 0;
    var qe_empty_check = 0;
    var dq_head = 0;
    var dq_mod_head = 0;
    var dq_entry_addr = 0;
    var eq_tail = 0;
    var eq_mod_tail = 0;
    var eq_entry_addr = 0;
    var rs_signalled = 0;
    var eo_client = 0;
    var eo_cli_data_io = 0;
    var eo_cli_capacity = 0;
    var eo_cli_max_addr = 0;

    while (i < local_num_clients) {
        var notify_addr = pnk_mem_ptr(NOTIFY_CLIENTS_BASE + i);
        !st32 notify_addr, 0;
        i = i + 1;
    }

    while (reprocess) {
        var drv_empty = 0;
        NET_QUEUE_EMPTY(local_drv_free_queue, local_drv_capacity, drv_empty);

        while (true) {
            if (drv_empty) { break; }

            var phys_addr = 0;
            var len = 0;
            NET_DEQUEUE(local_drv_free_queue, local_drv_capacity, phys_addr, len);

            var client = 0;
            var offset = 0;
            EXTRACT_OFFSET(phys_addr, local_num_clients, client, offset)

            if (client < 0) {
                break;
            }

            var cli_queue_base = CLI_QUEUE_BASE + client * 4;
            var cli_free_queue = pnk_mem(cli_queue_base);
            var cli_capacity = pnk_mem(cli_queue_base + 2);

            NET_ENQUEUE(cli_free_queue, offset, len, cli_capacity);

            var notify_addr = pnk_mem_ptr(NOTIFY_CLIENTS_BASE + client);
            !st32 notify_addr, 1;

            NET_QUEUE_EMPTY(local_drv_free_queue, local_drv_capacity, drv_empty);
        }

        NET_REQUEST_SIGNAL(local_drv_free_queue);
        reprocess = 0;

        NET_QUEUE_EMPTY(local_drv_free_queue, local_drv_capacity, drv_empty);
        if (drv_empty == 0) {
            NET_CANCEL_SIGNAL(local_drv_free_queue);
            reprocess = 1;
        }
    }

    var client = 0;
    while (client < local_num_clients) {
        var notify_flag = pnk_mem(NOTIFY_CLIENTS_BASE + client);

        if (notify_flag) {
            var cli_queue_base = CLI_QUEUE_BASE + client * 4;
            var cli_free_queue = pnk_mem(cli_queue_base);
            var cli_capacity = pnk_mem(cli_queue_base + 2);
            var signal_required = 0;
            NET_REQUIRE_SIGNAL(cli_free_queue, signal_required);

            if (signal_required) {
                NET_CANCEL_SIGNAL(cli_free_queue);
                var cli_conn_id = pnk_mem(CLI_CONN_ID_BASE + client);
                sddf_notify(cli_conn_id)
            }
        }

        client = client + 1;
    }

    return 0;
}

export fun notified(1 ch) {
    tx_return();
    tx_provide();

    return 0;
}
