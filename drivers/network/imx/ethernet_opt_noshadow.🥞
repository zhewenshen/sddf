
#define IRQ_CH                  pnk_mem(1)
#define RX_CH                   pnk_mem(2)
#define TX_CH                   pnk_mem(3)

#define NET_RX_FREE             pnk_mem(4)
#define NET_RX_ACTIVE           pnk_mem(5)
#define NET_RX_CAPACITY         pnk_mem(6)
#define NET_TX_FREE             pnk_mem(7)
#define NET_TX_ACTIVE           pnk_mem(8)
#define NET_TX_CAPACITY         pnk_mem(9)

#define HW_RX_TAIL_SLOT         pnk_mem_ptr(10)
#define HW_RX_HEAD_SLOT         pnk_mem_ptr(11)
#define HW_RX_TAIL              pnk_mem(10)
#define HW_RX_HEAD              pnk_mem(11)
#define HW_RX_CAPACITY          pnk_mem(12)
#define HW_RX_DESCR             pnk_mem(13)
#define HW_RX_PTR               pnk_mem_ptr(10)

#define HW_TX_TAIL_SLOT         pnk_mem_ptr(14)
#define HW_TX_HEAD_SLOT         pnk_mem_ptr(15)
#define HW_TX_TAIL              pnk_mem(14)
#define HW_TX_HEAD              pnk_mem(15)
#define HW_TX_CAPACITY          pnk_mem(16)
#define HW_TX_DESCR             pnk_mem(17)
#define HW_TX_PTR               pnk_mem_ptr(14)

#define EIR_OFFSET              4
#define RDAR_OFFSET             16
#define TDAR_OFFSET             20

#define RXD_EMPTY               (1 << 15)
#define WRAP                    (1 << 13)
#define TXD_READY               (1 << 15)
#define TXD_ADDCRC              (1 << 10)
#define TXD_LAST                (1 << 11)
#define RDAR_RDAR               (1 << 24)
#define TDAR_TDAR               (1 << 24)

#define NETIRQ_RXF              (1 << 25)
#define NETIRQ_TXF              (1 << 27)
#define NETIRQ_EBERR            (1 << 22)
#define IRQ_MASK                (NETIRQ_RXF | NETIRQ_TXF | NETIRQ_EBERR)

#define ETH_REG                 pnk_mem(0)

#define DESCR_SIZE              8
#define MAX_PKT_SIZE            1536

var 1 irq_ch          = IRQ_CH;
var 1 rx_ch           = RX_CH;
var 1 tx_ch           = TX_CH;

var 1 eth_reg         = ETH_REG;
var 1 net_rx_free     = NET_RX_FREE;
var 1 net_rx_active   = NET_RX_ACTIVE;
var 1 net_rx_capacity = NET_RX_CAPACITY;
var 1 net_tx_free     = NET_TX_FREE;
var 1 net_tx_active   = NET_TX_ACTIVE;
var 1 net_tx_capacity = NET_TX_CAPACITY;

var 1 hw_rx_capacity  = HW_RX_CAPACITY;
var 1 hw_tx_capacity  = HW_TX_CAPACITY;
var 1 hw_rx_descr     = HW_RX_DESCR;
var 1 hw_tx_descr     = HW_TX_DESCR;

#define RX_HW_RING_EMPTY() (HW_RX_HEAD == HW_RX_TAIL)
#define TX_HW_RING_EMPTY() (HW_TX_HEAD == HW_TX_TAIL)
#define RX_HW_RING_FULL() ((HW_RX_TAIL - HW_RX_HEAD) == HW_RX_CAPACITY)
#define TX_HW_RING_FULL() ((HW_TX_TAIL - HW_TX_HEAD) == HW_TX_CAPACITY)

#define SET_RDAR() \
  var rdar_reg_addr = eth_reg + RDAR_OFFSET; \
  !st32 rdar_reg_addr, RDAR_RDAR

#define SET_TDAR() \
  var tdar_reg_addr = eth_reg + TDAR_OFFSET; \
  !st32 tdar_reg_addr, TDAR_TDAR

#define GET_EIR(eir_out) \
  var eir_full = 0; \
  var get_eir_reg = eth_reg + EIR_OFFSET; \
  !ld32 eir_full, get_eir_reg; \
  eir_out = eir_full & IRQ_MASK

#define SET_EIR(eir) \
  var set_eir_reg_addr = eth_reg + EIR_OFFSET; \
  !st32 set_eir_reg_addr, eir


#define RX_UPDATE_RING_SLOT(ring_addr, idx, phys, len, stat) \
  var rx_descr = hw_rx_descr + idx * DESCR_SIZE; \
  var rx_stat_tmp = stat; \
  if (idx + 1 == hw_rx_capacity) { \
    rx_stat_tmp = rx_stat_tmp | WRAP; \
  } \
  !st32 rx_descr + 4, phys; \
  !st16 rx_descr, len; \
  THREAD_MEMORY_RELEASE() \
  !st16 rx_descr + 2, rx_stat_tmp

#define TX_UPDATE_RING_SLOT(ring_addr, idx, phys, len, stat) \
  var tx_descr = hw_tx_descr + idx * DESCR_SIZE; \
  var tx_stat_tmp = stat; \
  if (idx + 1 == hw_tx_capacity) { \
    tx_stat_tmp = tx_stat_tmp | WRAP; \
  } \
  !st32 tx_descr + 4, phys; \
  !st16 tx_descr, len; \
  THREAD_MEMORY_RELEASE() \
  !st16 tx_descr + 2, tx_stat_tmp

#define NET_BUFF_DESC_SIZE  16
#define MAX_INT16           65535


#define NET_QUEUE_EMPTY(queue_ptr, capacity, result_out) \
  var qe_tail = 0; \
  var qe_head = 0; \
  !ld16 qe_tail, queue_ptr; \
  !ld16 qe_head, queue_ptr + 2; \
  var qe_empty_check = qe_tail - qe_head; \
  result_out = (qe_empty_check == 0)

#define NET_DEQUEUE(queue_ptr, capacity, io_out, len_out) \
  var dq_head = 0; \
  !ld16 dq_head, queue_ptr + 2; \
  var dq_mod_head = dq_head & (capacity - 1); \
  var dq_entry_addr = queue_ptr + 8 + dq_mod_head * NET_BUFF_DESC_SIZE; \
  !ldw io_out, dq_entry_addr; \
  !ld32 len_out, dq_entry_addr + 8; \
  dq_head = (dq_head + 1) & MAX_INT16; \
  !st16 queue_ptr + 2, dq_head

#define NET_ENQUEUE(queue_ptr, io_addr, len, capacity) \
  var eq_tail = 0; \
  !ld16 eq_tail, queue_ptr; \
  var eq_mod_tail = eq_tail & (capacity - 1); \
  var eq_entry_addr = queue_ptr + 8 + eq_mod_tail * NET_BUFF_DESC_SIZE; \
  !stw eq_entry_addr, io_addr; \
  !st32 eq_entry_addr + 8, len; \
  eq_tail = (eq_tail + 1) & MAX_INT16; \
  !st16 queue_ptr, eq_tail

#define NET_REQUEST_SIGNAL(queue_ptr) \
  !st32 queue_ptr + 4, 0

#define NET_CANCEL_SIGNAL(queue_ptr) \
  !st32 queue_ptr + 4, 1

#define NET_REQUIRE_SIGNAL(queue_ptr, result_out) \
  var rs_signalled = 0; \
  !ld32 rs_signalled, queue_ptr + 4; \
  result_out = !rs_signalled

fun rx_provide()
{
  var reprocess = true;

  while (reprocess)
  {
    while (true)
    {
      var full = RX_HW_RING_FULL();
      if (full) { break; }

      var empty = 0;
      NET_QUEUE_EMPTY(net_rx_free, net_rx_capacity, empty);
      if (empty) { break; }

      var io_addr = 0;
      var len = 0;
      NET_DEQUEUE(net_rx_free, net_rx_capacity, io_addr, len);

      var idx = HW_RX_TAIL & (hw_rx_capacity - 1);

      RX_UPDATE_RING_SLOT(HW_RX_PTR, idx, io_addr, 0, RXD_EMPTY);

      st HW_RX_TAIL_SLOT, (HW_RX_TAIL + 1);
      SET_RDAR();
    }

    if (!RX_HW_RING_FULL()) {
        NET_REQUEST_SIGNAL(net_rx_free);
    } else {
        NET_CANCEL_SIGNAL(net_rx_free);
    }

    NET_REQUEST_SIGNAL(net_rx_free);
    reprocess = false;

    var empty = 0;
    NET_QUEUE_EMPTY(net_rx_free, net_rx_capacity, empty);
    var full = RX_HW_RING_FULL();
    if ((!empty) && (!full)) {
      NET_CANCEL_SIGNAL(net_rx_free);
      reprocess = true;
    }
  }
  return 0;
}

fun tx_provide()
{
  var reprocess = true;

  while (reprocess)
  {
    while (true)
    {
      var full = TX_HW_RING_FULL();
      if (full) { break; }

      var empty = 0;
      NET_QUEUE_EMPTY(net_tx_active, net_tx_capacity, empty);
      if (empty) { break; }

      var io_addr = 0;
      var len = 0;
      NET_DEQUEUE(net_tx_active, net_tx_capacity, io_addr, len);

      var idx = HW_TX_TAIL & (hw_tx_capacity - 1);

      TX_UPDATE_RING_SLOT(HW_TX_PTR, idx, io_addr, len, TXD_READY | TXD_ADDCRC | TXD_LAST);

      st HW_TX_TAIL_SLOT, (HW_TX_TAIL + 1);
      SET_TDAR();
    }

    NET_REQUEST_SIGNAL(net_tx_active);
    reprocess = false;

    var full = TX_HW_RING_FULL();
    var empty = 0;
    NET_QUEUE_EMPTY(net_tx_active, net_tx_capacity, empty);
    if ((!empty) && (!full)) {
      NET_CANCEL_SIGNAL(net_tx_active);
      reprocess = true;
    }
  }
  return 0;
}

fun rx_return()
{
  var packets_transferred = false;

  while (true)
  {
    var empty = RX_HW_RING_EMPTY();
    if (empty) { break; }

    var idx = HW_RX_HEAD & (hw_rx_capacity - 1);

    var dscr_addr = hw_rx_descr + idx * DESCR_SIZE;
    var stat = 0;
    !ld16 stat, dscr_addr + 2;
    if (stat & RXD_EMPTY) { break; }

    THREAD_MEMORY_ACQUIRE()

    var len = 0;
    !ld16 len, dscr_addr;
    var io_addr = 0;
    !ld32 io_addr, dscr_addr + 4;

    NET_ENQUEUE(net_rx_active, io_addr, len, net_rx_capacity);
    packets_transferred = true;

    st HW_RX_HEAD_SLOT, (HW_RX_HEAD + 1);
  }

  var to_signal = 0;
  NET_REQUIRE_SIGNAL(net_rx_active, to_signal);
  if (packets_transferred && to_signal) {
    NET_CANCEL_SIGNAL(net_rx_active);
    microkit_notify(rx_ch)
  }

  return 0;
}

fun tx_return()
{
  var enqueued = false;

  while (true)
  {
    var empty = TX_HW_RING_EMPTY();
    if (empty) { break; }

    var idx = HW_TX_HEAD & (hw_tx_capacity - 1);

    var dscr_addr = hw_tx_descr + idx * DESCR_SIZE;
    var stat = 0;
    !ld16 stat, dscr_addr + 2;
    if (stat & TXD_READY) { break; }

    THREAD_MEMORY_ACQUIRE()

    var io_addr = 0;
    !ld32 io_addr, dscr_addr + 4;
    NET_ENQUEUE(net_tx_free, io_addr, 0, net_tx_capacity);

    enqueued = true;
    st HW_TX_HEAD_SLOT, (HW_TX_HEAD + 1);
  }

  var to_signal = 0;
  NET_REQUIRE_SIGNAL(net_tx_free, to_signal);
  if (enqueued && to_signal) {
    NET_CANCEL_SIGNAL(net_tx_free);
    microkit_notify(tx_ch)
  }

  return 0;
}

fun main() {
  rx_provide();
  tx_provide();
  return 0;
}

fun handle_irq() {
  var eir = 0;
  GET_EIR(eir);
  SET_EIR(eir);

  while (eir & IRQ_MASK)
  {
    if (eir & NETIRQ_TXF) {
      tx_return();
      tx_provide();
    }
    if (eir & NETIRQ_RXF) {
      rx_return();
      rx_provide();
    }
    if (eir & NETIRQ_EBERR) {
        // error
    }
    GET_EIR(eir);
    SET_EIR(eir);
  }
  return 0;
}


export fun notified(1 ch)
{
  if (ch == irq_ch) {
    handle_irq();
    microkit_deferred_irq_ack(ch)
    return 0;
  }
  if (ch == rx_ch) {
    rx_provide();
    return 0;
  }
  if (ch == tx_ch) {
    tx_provide();
    return 0;
  }
  return 0;
}
